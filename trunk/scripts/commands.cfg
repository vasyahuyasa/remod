//simple commands are here

//registercommand(const char* cmd_name, const char* cmd_func, int cmd_perm, const char* cmd_descr, const char* cmd_help)

cmd_help = [
	if (=s $arg2 "") [
		res = ""
		perm = (? (isadmin $arg1) (? (ismaster $arg1) 3 2) 1)
		cmd_list = (commandslist $perm)

		loop i (listlen $cmd_list) [   
			res = (concatword $res " #" (at $cmd_list $i)) 	
		]
		pm $arg1 (format "^f1Server commands:^f0%1" $res) 
	] [
		help = (commandhelp $arg2)
		if (=s $help "") [
			(unknown_command $arg1 $arg2)
		] [
			pm $arg1 (usage $arg2)
		]
	]
]
registercommand "help" cmd_help 1 "|s" "^help (command) ^f1Shows info about command or lists server commands"


cmd_pm = [
	pm $arg2 (format "^f3(private) %1(%2): ^f1%3" (getname $arg1) $arg1 $arg3)
]
registercommand "pm" cmd_pm 1 "cs" "pm [cn] [message] ^f1Sends private message to user" //parameters are passed to pm are cn and string, both of these params are obligatory


cmd_persist = [
	if (!=s $arg2 "") [
		persist $arg2	
	]                
	say (concatword "^f1Shuffle teams is ^f0" (? $persist "disabled" "enabled" ))
]
registercommand "persist" cmd_persist 2 "|b" "persist [1|0] ^f1Persists teams"



cmd_specall = [
	all = (allplayers)
	looplist cn $all [	
		spectator $arg2 $cn
	]
	say (concatword "^f1All players were ^f0" (? $arg2 "spectated" "unspectated" ))
]
registercommand "specall" cmd_specall 2 "b" "specall [1|0] ^f1Spectate or unspectate all players"



cmd_restart = [
	(map (getmap))
	say ("^f1Game was restarted")
]
registercommand "restart" cmd_restart 2 "" "restart ^f1Restart current game"



cmd_getip = [
	say (format "^f1%1(%2) ^f0%3" (getname $arg2) $arg2 (getip $arg2))
]
registercommand "getip" cmd_getip 2 "c""getip [cn] ^f1Show player's ip"



cmd_version = [
	pm $arg1 (format "^f1Software version: ^f0%1" (version))
]
registercommand "version" cmd_version 1 "" "version ^f1Server's version"


//mute player
cmd_mute = [
	if (= (ismuted $arg2) 1) [
		pm $arg1 (format "^f1Player ^f0%1(%2) ^f1is already ^f0%3" (getname $arg2) $arg2 "muted")
	] [
		mute $arg2 1
		say (format "^f1Player ^f0%1(%2) ^f1awas ^f0%3" (getname $arg2) $arg2 "muted")
		pm $arg2 "^f1You was ^f0muted^f1. Nobody hears your scream"

	]
]
registercommand "mute" cmd_mute 2 "c" "mute [cn] ^f1Mute player"



cmd_unmute = [
	if (= (ismuted $arg2) 0) [
		pm $arg1 (format "^f1Player ^f0%1(%2) ^f1is not ^f0%3" (getname $arg2) $arg2 "muted")
	] [
		mute $arg2 0
		say (format "^f1Player ^f0%1(%2) ^f1awas ^f0%3" (getname $arg2) $arg2 "unmuted")
		pm $arg2 "^f1You was ^f0unmuted^f1. Please be polite" 

	]	
]
registercommand "unmute" cmd_unmute 2 "c" "unmute [cn] ^f1Unmute player"


//time
cmd_time = [
	newtime = (* $arg2 60000)
	if (> $newtime 0 ) [
		setroundtime (+ $gamemillis $newtime )
		say (format "^f1Time limit set to ^f0%1 min"  $arg2)
	]	
]
registercommand "time" cmd_time 2 "i" "time [min] ^f1Time limit for current round"

