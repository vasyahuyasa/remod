// $arg1 - irc user
// $arg2, $arg3 - "p a r a m s"
// 0 - everyone, 1 - voice, 2 - op, 3 - special

irccmd_help = [
	if (=s $arg2 "") [
		perm = (? (ircisvoice $arg1) (? (ircisop $arg1) 3 2) 0)
		cmd_list = (irc_commandslist $perm)

		ircsay (format "%1: IRC commands: %2" $arg1 $cmd_list) 
	] [
		help = (irc_commandhelp $arg2)
		if (=s $help "") [
			(unknown_command $arg1 $arg2)
		] [
			ircsay (format "%1: %2" $arg1 (usage $arg2))
		]
	]
]
irc_registercommand "help" irccmd_help 1 "|s" "help (command). Shows info about command or lists IRC commands"



irccmd_say = [
	say (format "^f0(remote) %1: ^f3%2" $arg1 $arg2)                                                           	
]
irc_registercommand "say" irccmd_say 2 "s" "say [msg]. Send message to server"

irccmd_pm = [
	pm $arg2 (format "^f0(remote, private) %1: ^f3%2" $arg1 $arg3)                                                           	
]
irc_registercommand "pm" irccmd_pm 2 "cs" "pm [cn] [msg]. Send private message to player"


irccmd_who = [
	if (> (numclients) 0) [
		all = (allplayers)          
		
		plist = ""
		plays = "" 
		specs = "" 
		
		looplist cn $all [
			if (isspectator $cn) [
				specs = (concat $specs (format "%1(%2)" (getname $cn) $cn))
			] [
				plays = (concat $plays (format "%1(%2)" (getname $cn) $cn))
			]
		]
		
		if (!=s $plays "") [ plays = (concat "Players:" (prettylist $plays)) ] 
		if (!=s $specs "") [ specs = (concat "Spectators:" (prettylist $specs)) ]
		 
		plist = (concat $plays $specs)		
		ircsay  $plist		
	] [
		ircsay "No one connected"
	] 		                                                           	
]
irc_registercommand "who" irccmd_who 0 "" "List players"



irccmd_stats = [
	infoline = ""
	if (=s $arg2 "") [  // show game info
		infoline = (modetostr (getmode))	// Capture
		infoline = (concat $infoline (getmap)) // Capture nevil_c  
		infoline = (concat $infoline (format "players: %1/%2" (numclients) $maxclients)) // Capture nevil_c players: 10/24
		infoline = (concat $infoline (formatmillis "time left: %m:%s" (- $gamelimit $gamemillis))) // Capture nevil_c players: 10/24 time left: 09:50
        if (isinlist $TEAMMODES (modetostr (getmode))) [ infoline = (concat $infoline (format "| good: %1 - evil: %2" (getteamscores "good") (getteamscores "evil"))) ]	// Capture nevil_c players: 10/24 time left: 09:50 | good: 42 evil: 26
        
        // Best 3 
        all = (allplayers)
        fpline = "|"
        loop i	3 [
        	n = (+ $i 1)
        	looplist cn $all [     
        		if (= (getrank $cn) $n) [
        			fpline = (concat $fpline (format "%1. %2 (%3/%4 %5%%%%)" $n (getname $cn) (getfrags $cn) (getdeaths $cn) (getaccuracy $cn)))        		
        		] 
        	]               		        	        	        	
        ]
        
        infoline = (concat $infoline $fpline) // Capture nevil_c players: 10/24 time left: 09:50 | good: 42 evil: 26 | 1. degrave (67/30 24%) 2. unnamed (2/60 5%)
	] [ // Player info
		if (playerexists $arg2) [
			infoline = (format "stast: %1 %2/%3 %4%%%% teamkils: %5 flags: %6 time online: %7" (getname $arg2) (getfrags $arg2) (getdeaths $arg2) (getaccuracy $arg2) (getteamkills $arg2) (getflags $arg2) (formatmillis "%h:%m:%s" (getonline $arg2)))
		] [
			infoline = (format "Player '%1' does not exists")
		]
	]
	
	ircsay $infoline
]
irc_registercommand "stats" irccmd_stats 0 "|c" "stats [cn]. Show game or player info"
                                                 
                                                 
                                                 
irccmd_kick = [
	kick $arg2
]             
irc_registercommand "kick" irccmd_kick 2 "c" "kick [cn]. Kick player"


 
irccmd_spec = [ 
	spectator 1 $arg2
]
irc_registercommand "spec" irccmd_spec 2 "c" "spec [cn]. Force player to spectate"


irccmd_unspec = [ 
	spectator 0 $arg2
]
irc_registercommand "unspec" irccmd_unspec 2 "c" "unspec [cn]. Unspectate player"


