// $arg1 - irc user
// $arg2, $arg3 - "p a r a m s"
// 0 - everyone, 1 - voice, 2 - op, 3 - special

usage_irccmd_say = [result "say [msg]. Send message to server"]
irccmd_say = [
	say (format "^f0(remote) %1: ^f3%2" $arg1 $arg2)                                                           	
]
irc_registercommand "say" irccmd_say "s" 2



usage_irccmd_who = [result "Show player list"]
irccmd_who = [
	if (> (numclients) 0) [
		all = (allplayers)          
		
		plist = ""
		plays = "" 
		specs = "" 
		
		looplist cn $all [
			if (isspectator $cn) [
				specs = (concat $specs (format "%1(%2)" (getname $cn) $cn))
			] [
				plays = (concat $plays (format "%1(%2)" (getname $cn) $cn))
			]
		]
		
		if (!=s $plays "") [ plays = (concat "Players:" (prettylist $plays)) ] 
		if (!=s $specs "") [ specs = (concat "Spectators:" (prettylist $specs)) ]
		 
		plist = (concat $plays $specs)		
		ircsay  $plist		
	] [
		ircsay "No one connected"
	] 		                                                           	
]
irc_registercommand "who" irccmd_who "" 0



usage_irccmd_stats = [result "stats [cn]. Show game or player info"]
irccmd_stats = [
	infoline = ""
	if (=s $arg2 "") [  // show game info
		infoline = (modetostr (getmode))	// Capture
		infoline = (concat $infoline (getmap)) // Capture nevil_c  
		infoline = (concat $infoline (format "players: %1/%2" (numclients) $maxclients)) // Capture nevil_c players: 10/24
		infoline = (concat $infoline (formatmillis "time left: %m:%s" (- $gamelimit $gamemillis))) // Capture nevil_c players: 10/24 time left: 09:50
        if (isinlist $TEAMMODES (modetostr (getmode))) [ infoline = (concat $infoline (format "| good: %1 - evil: %2" (getteamscores "good") (getteamscores "evil"))) ]	// Capture nevil_c players: 10/24 time left: 09:50 | good: 42 evil: 26
        
        // Best 3 
        all = (allplayers)
        fpline = "|"
        loop i	3 [
        	n = (+ $i 1)
        	looplist cn $all [     
        		if (= (getrank $cn) $n) [
        			fpline = (concat $fpline (format "%1. %2 (%3/%4 %5%%%%)" $n (getname $cn) (getfrags $cn) (getdeaths $cn) (getaccuracy $cn)))        		
        		] 
        	]               		        	        	        	
        ]
        
        infoline = (concat $infoline $fpline) // Capture nevil_c players: 10/24 time left: 09:50 | good: 42 evil: 26 | 1. degrave (67/30 24%) 2. unnamed (2/60 5%)
	] [ // Player info
		if (playerexists $arg2) [
			infoline = (format "stast: %1 %2/%3 %4%%%% teamkils: %5 flags: %6 time online: %7" (getname $arg2) (getfrags $arg2) (getdeaths $arg2) (getaccuracy $arg2) (getteamkills $arg2) (getflags $arg2) (formatmillis "%h:%m:%s" (getonline $arg2)))
		] [
			infoline = (format "Player '%1' does not exists")
		]
	]
	
	ircsay $infoline
]
irc_registercommand "stats" irccmd_stats "|c" 0
                                                 
                                                 
                                                 
usage_irccmd_kick = [ result "kick [cn]. Kick player" ]
irccmd_kick = [
	kick $arg2
]             
irc_registercommand "kick" irccmd_kick "c" 2


 
usage_irccmd_spec = [ result "spec [cn]. Force player to spectate" ]
irccmd_spec = [ 
	spectator 1 $arg2
]
irc_registercommand "spec" irccmd_spec "c" 2



usage_irccmd_unspec = [ result "unspec [cn]. Unspectate player" ]
irccmd_unspec = [ 
	spectator 0 $arg2
]
irc_registercommand "unspec" irccmd_unspec "c" 2
                                 